Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION OBNECRO_SACRIFICE_SOURCES
IF
SavegameLoading(_,_,_,_)
AND
DB_OBNECRO_Bloodtide(_SkillId, _Percentage)
THEN
NOT DB_OBNECRO_Bloodtide(_SkillId, _Percentage);

IF
GameStarted(_, _)
THEN
PROC_OBNECRO_ConfigureBloodtideSkills();

IF
SavegameLoaded(_,_,_,_)
THEN
PROC_OBNECRO_ConfigureBloodtideSkills();

PROC
PROC_OBNECRO_ConfigureBloodtideSkills()
THEN
PROC_OBNECRO_AddBloodtideSkill("Target_OdinNECRO_SanguineTorrent", 16);
PROC_OBNECRO_AddBloodtideSkill("Target_OdinNECRO_Enemy_SanguineTorrent", 11);
PROC_OBNECRO_AddBloodtideSkill("Target_OdinNECRO_Translocate", 11);
PROC_OBNECRO_AddBloodtideSkill("Projectile_OdinNECRO_BloodBarrage", 16);
PROC_OBNECRO_AddBloodtideSkill("Projectile_OdinNECRO_Enemy_BloodBarrage", 11);
PROC_OBNECRO_AddBloodtideSkill("Rain_Blood", 6);
PROC_OBNECRO_AddBloodtideSkill("Rain_EnemyBlood", 6);
PROC_OBNECRO_AddBloodtideSkill("Shout_OdinNECRO_Bloodletting", 6);
PROC_OBNECRO_AddBloodtideSkill("Shout_OdinNECRO_Hemoplague", 21);
PROC_OBNECRO_AddBloodtideSkill("Shout_OdinNECRO_Enemy_Hemoplague", 16);
PROC_OBNECRO_AddBloodtideSkill("Storm_Blood", 26);

PROC
PROC_OBNECRO_AddBloodtideSkill((STRING)_SkillId, (INTEGER)_Percentage)
AND
NOT DB_OBNECRO_Bloodtide(_SkillId, _Percentage)
THEN
DB_OBNECRO_Bloodtide(_SkillId, _Percentage);

//Extra 1% added due to rounding issues.
IF
CharacterUsedSkill(_Character, _SkillId,_,_)
AND
DB_OBNECRO_Bloodtide(_SkillId, _Percentage)
THEN
ODINNECRO_SAC(_Character, _Percentage);

//Status for Death Wish usage on allies.
IF
CharacterStatusApplied(_Character, "DEATH_WISH", _)
THEN
ODINNECRO_SAC(_Character, 11);
//END_REGION

//REGION OBNECRO_SACRIFICE_CALCULATE
//Calculates health percentage value and calls other PRCOCs
PROC
ODINNECRO_SAC((CHARACTERGUID)_Character, (INTEGER)_SacPercentage)
AND
IntegerSubtract(100, _SacPercentage, _TargetPercentage)
AND
CharacterGetHitpointsPercentage(_Character, _PercentageHP)
AND
Integer(_PercentageHP, _INTPercentageHP)
AND
IntegerProduct(_INTPercentageHP, _TargetPercentage, _NewPercentageHP)
AND
IntegerDivide(_NewPercentageHP, 100, _PrepPercentageHP)
AND
Real(_PrepPercentageHP, _FinalPercentageHP)
THEN
ODINNECRO_SET_PERCENTAGE(_Character, _FinalPercentageHP);
ODINNECRO_BLOODARMOUR(_Character, _SacPercentage);
//END_REGION

//REGION OBNECRO_SACRIFICE_SET_METHODS
//Standard path for sacrifice calculation.
PROC
ODINNECRO_SET_PERCENTAGE((CHARACTERGUID)_Character, (REAL)_FinalPercentageHP)
AND
_FinalPercentageHP > 0.0
THEN
CharacterSetHitpointsPercentage(_Character, _FinalPercentageHP);

//Only fires if Vitality is <1% and prevents character dying this way.
PROC
ODINNECRO_SET_PERCENTAGE((CHARACTERGUID)_Character, (REAL)_FinalPercentageHP)
AND
_FinalPercentageHP < 1.0
THEN
CharacterSetHitpointsPercentage(_Character, 0.1);
//END_REGION

//REGION OBNECRO_SACRIFICE_OTHER_USES
//Calculates armour restoration and sets it onto character w/ Blood Armour status.
PROC
ODINNECRO_BLOODARMOUR((CHARACTERGUID)_Character, (INTEGER)_RestorePercentage)
AND
HasActiveStatus(_Character, "OdinNECRO_BLOODARMOUR", 1)
AND
CharacterGetArmorPercentage(_Character, _CurrentPercentage)
AND
Integer(_CurrentPercentage, _ICurrentPercentage)
AND
IntegerSum(_RestorePercentage, _ICurrentPercentage, _NewPercentage)
AND
Real(_NewPercentage, _RealPercentage)
THEN
CharacterSetArmorPercentage(_Character, _RealPercentage);
//END_REGION
EXITSECTION

ENDEXITSECTION
